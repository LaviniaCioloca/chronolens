allprojects {
    group 'org.metanalysis'
    version '0.1.5'

    buildscript {
        ext {
            kotlin_version = '1.1.2'
            dokka_version = '0.9.13'
            jackson_version = '2.8.7'
            jdt_version = '3.12.3'
            junit_version = '4.12'
            shadow_version = '1.2.4'
        }

        repositories {
            mavenCentral()
            jcenter()
        }
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'jacoco'
}

subprojects {
    buildscript {
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
            classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
            classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        }
    }

    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "junit:junit:$junit_version"
    }

    jar {
        from rootProject.file('LICENSE')
        from rootProject.file('NOTICE')
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    jacocoTestReport {
        dependsOn test

        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn {
        subprojects*.jacocoTestReport
    }

    executionData fileTree(project.rootDir.absolutePath)
            .include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}
