apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(':metanalysis-core')
    runtime project(':metanalysis-git')
    runtime project(':metanalysis-java')
}

ext.mainClassName = 'org.metanalysis.cli.Main'
jar.manifest.attributes 'Main-Class': project.mainClassName

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

javadocJar.dependsOn dokkaJavadoc

task releaseUnix(dependsOn: shadowJar) << {
    def artifact = new File(rootProject.buildDir, rootProject.name)
    delete(artifact)
    files('unix-wrapper.sh', shadowJar.archivePath).forEach { f ->
        artifact << f.bytes
    }
    artifact.setExecutable(true, false)
}

task release(dependsOn: releaseUnix)
